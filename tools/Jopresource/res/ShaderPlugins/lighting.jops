// Jopnal lighting shader plugin file
//
// Jopnal license applies.

// Required uniforms for lighting calculations
//
// Required by both lighting and shadows
//
#plugin <Jopnal/DefaultLighting/Uniforms>

    // Material
    struct Material
    {
        vec4 ambient;
        vec4 diffuse;
        vec4 specular;
        vec4 emission;
    
        float shininess;
        float reflectivity;
    };
    uniform Material u_Material;

    #if JMAT_MAX_POINT_LIGHTS > 0

        // Point lights
        struct PointLightInfo
        {
            bool enabled;

            // Position
            vec3 position;

            // Intensities
            vec3 ambient;
            vec3 diffuse;
            vec3 specular;

            // Attenuation
            // x = constant
            // y = linear
            // z = quadratic
            vec3 attenuation;

            // Shadow map info
            bool castShadow;    ///< Cast shadows?
            float farPlane;     ///< The light's far plane
        };
        uniform PointLightInfo u_PointLights[JMAT_MAX_POINT_LIGHTS];
        uniform samplerCube u_PointLightShadowMaps[JMAT_MAX_POINT_LIGHTS];
        uniform int u_NumPointLights;

    #endif

    #if JMAT_MAX_SPOT_LIGHTS > 0

        // Spot lights
        struct SpotLightInfo
        {
            bool enabled;

            // Position
            vec3 position;

            // Direction
            vec3 direction;

            // Intensities
            vec3 ambient;
            vec3 diffuse;
            vec3 specular;

            // Attenuation
            vec3 attenuation;

            // Cutoff
            // x = inner
            // y = outer
            vec2 cutoff;

            // Shadow map info
            bool castShadow;        ///< Cast shadows?
            mat4 lsMatrix;          ///< Light space matrix
        };
        uniform SpotLightInfo u_SpotLights[JMAT_MAX_SPOT_LIGHTS];
        uniform sampler2D u_SpotLightShadowMaps[JMAT_MAX_SPOT_LIGHTS];
        uniform int u_NumSpotLights;

    #endif

    #if JMAT_MAX_DIRECTIONAL_LIGHTS > 0

        // Directional lights
        struct DirectionalLightInfo
        {
            bool enabled;

            // Direction
            vec3 direction;

            // Intensities
            vec3 ambient;
            vec3 diffuse;
            vec3 specular;

            // No attenuation for directional lights

            // Shadow map info
            bool castShadow;        ///< Cast shadows?
            mat4 lsMatrix;          ///< Light space matrix
        };
        uniform DirectionalLightInfo u_DirectionalLights[JMAT_MAX_DIRECTIONAL_LIGHTS];
        uniform sampler2D u_DirectionalLightShadowMaps[JMAT_MAX_DIRECTIONAL_LIGHTS];
        uniform int u_NumDirectionalLights;

    #endif

#pluginend

// Default lighting functions
//
//
#plugin <Jopnal/DefaultLighting/Lighting>

    #if JMAT_MAX_POINT_LIGHTS > 0
        
        // Point light calculation
        void jop_CalculatePointLight(const in int index, out vec3 ambient, out vec3 diffuse, out vec3 specular)
        {
            PointLightInfo l = u_PointLights[index];

            #if __VERSION__ < 300

                if (!l.enabled)
                {
                    ambient = vec3(0.0);
                    diffuse = vec3(0.0);
                    specular = vec3(0.0);
                }

            #endif

            // Ambient impact
            ambient = l.ambient * vec3(u_Material.ambient);

            // Normal vector
            vec3 norm = normalize(vf_Normal);

            // Direction from fragment to light
            vec3 lightDir = normalize(l.position - vf_FragPosition);

            // Diffuse impact
            float diff = max(dot(norm, lightDir), 0.0);
            diffuse = diff * l.diffuse * vec3(u_Material.diffuse);

            // Direction from fragment to eye
            vec3 viewDir = normalize(u_CameraPosition - vf_FragPosition);

            // Calculate reflection direction (use a half-way vector)
            vec3 reflectDir = normalize(lightDir + viewDir);

            float shininess = max(1.0, u_Material.shininess

            #ifdef JMAT_GLOSSMAP
                * JOP_TEXTURE_2D(u_GlossMap, vf_TexCoords).a
            #endif
            );

            // Specular impact
            float spec = (8.0 + shininess) / (8.0 * 3.14159265) /*<< energy conservation */ * pow(max(dot(norm, reflectDir), 0.0), shininess);

            specular = l.specular * spec * vec3(u_Material.specular)

            #ifdef JMAT_SPECULARMAP
                * vec3(JOP_TEXTURE_2D(u_SpecularMap, vf_TexCoords))
            #endif
            ;

            // Attenuation
            float dist = length(l.position - vf_FragPosition);
            float attenuation = 1.0 / (l.attenuation.x + l.attenuation.y * dist + l.attenuation.z * (dist * dist));
            ambient *= attenuation; diffuse *= attenuation; specular *= attenuation;

            // Shadow calculation
            if (l.castShadow && u_ReceiveShadows)
            {
                float shadow = jop_CalculatePointShadow(lightDir, l.farPlane, u_PointLightShadowMaps[index]);

                diffuse *= (1.0 - shadow);
                specular *= (1.0 - shadow);
            }
        }

    #endif

    #if JMAT_MAX_SPOT_LIGHTS > 0

        // Spot light calculation
        void jop_CalculateSpotLight(const in int index, out vec3 ambient, out vec3 diffuse, out vec3 specular)
        {
            SpotLightInfo l = u_SpotLights[index];

            #if __VERSION__ < 300

                if (!l.enabled)
                {
                    ambient = vec3(0.0);
                    diffuse = vec3(0.0);
                    specular = vec3(0.0);
                }

            #endif

            // Ambient impact
            ambient = l.ambient * vec3(u_Material.ambient);

            // Normal vector
            vec3 norm = normalize(vf_Normal);

            // Direction from fragment to light
            vec3 lightDir = normalize(l.position - vf_FragPosition);

            // Diffuse impact
            float diff = max(dot(norm, lightDir), 0.0);
            diffuse = l.diffuse * diff * vec3(u_Material.diffuse);

            // Direction from fragment to eye
            vec3 viewDir = normalize(u_CameraPosition - vf_FragPosition);

            // Calculate reflection direction (use a half-way vector)
            vec3 reflectDir = normalize(lightDir + viewDir);

            float shininess = max(1.0, u_Material.shininess

            #ifdef JMAT_GLOSSMAP
                * JOP_TEXTURE_2D(u_GlossMap, vf_TexCoords).a
            #endif
            );

            // Specular impact
            float spec = (8.0 + shininess) / (8.0 * 3.14159265) /*<< energy conservation */ * pow(max(dot(norm, reflectDir), 0.0), shininess);

            specular = l.specular * spec * vec3(u_Material.specular)

            #ifdef JMAT_SPECULARMAP
                * vec3(JOP_TEXTURE_2D(u_SpecularMap, vf_TexCoords))
            #endif
            ;

            // Spotlight soft edges
            float theta = dot(lightDir, normalize(-l.direction));
            float epsilon = (l.cutoff.x - l.cutoff.y);
            float intensity = clamp((theta - l.cutoff.y) / epsilon, 0.0, 1.0);
            ambient *= intensity; diffuse *= intensity; specular *= intensity;

            // Attenuation
            float dist = length(l.position - vf_FragPosition);
            float attenuation = 1.0 / (l.attenuation.x + l.attenuation.y * dist + l.attenuation.z * (dist * dist));
            ambient *= attenuation; diffuse *= attenuation; specular *= attenuation;

            // Shadow calculation
            if (l.castShadow && u_ReceiveShadows)
            {
                vec4 tempCoords = l.lsMatrix * vec4(vf_FragPosition, 1.0);

                float shadow = jop_CalculateDirSpotShadow((tempCoords.xyz / tempCoords.w) * 0.5 + 0.5, norm, lightDir, u_SpotLightShadowMaps[index]);

                diffuse *= (1.0 - shadow);
                specular *= (1.0 - shadow);
            }
        }

    #endif

    #if JMAT_MAX_DIRECTIONAL_LIGHTS > 0

        // Directional light calculation
        void jop_CalculateDirectionalLight(const in int index, out vec3 ambient, out vec3 diffuse, out vec3 specular)
        {
            DirectionalLightInfo l = u_DirectionalLights[index];

            #if __VERSION__ < 300

                if (!l.enabled)
                {
                    ambient = vec3(0.0);
                    diffuse = vec3(0.0);
                    specular = vec3(0.0);
                }

            #endif

            // Ambient impact
            ambient = l.ambient * vec3(u_Material.ambient);

            // Normal vector
            vec3 norm = normalize(vf_Normal);

            // Direction from light to fragment.
            // Directional light shines infinitely in the same direction,
            // so no need to take fragment position into account
            vec3 lightDir = normalize(-l.direction);

            // Diffuse impact
            float diff = max(dot(norm, lightDir), 0.0);
            diffuse = l.diffuse * diff * vec3(u_Material.diffuse);

            // Direction from fragment to eye
            vec3 viewDir = normalize(u_CameraPosition - vf_FragPosition);

            // Calculate reflection direction (use a half-way vector)
            vec3 reflectDir = normalize(lightDir + viewDir);

            float shininess = max(1.0, u_Material.shininess

            #ifdef JMAT_GLOSSMAP
                * JOP_TEXTURE_2D(u_GlossMap, vf_TexCoords).a
            #endif
            );

            // Specular impact
            float spec = (8.0 + shininess) / (8.0 * 3.14159265) * pow(max(dot(norm, reflectDir), 0.0), shininess);

            specular = l.specular * spec * vec3(u_Material.specular)

            #ifdef JMAT_SPECULARMAP
                * vec3(JOP_TEXTURE_2D(u_SpecularMap, vf_TexCoords))
            #endif
            ;

            // No attenuation calculations here
            // Directional Light is infinite, Directional Light is eternal

            // Shadow calculation
            if (l.castShadow && u_ReceiveShadows)
            {
                float shadow = jop_CalculateDirSpotShadow(vec3(l.lsMatrix * vec4(vf_FragPosition, 1.0)) * 0.5 + 0.5, norm, lightDir, u_DirectionalLightShadowMaps[index]);

                diffuse *= (1.0 - shadow);
                specular *= (1.0 - shadow);
            }
        }

    #endif

#pluginend

//
//
//
#plugin <Jopnal/DefaultLighting/Shadows>

    #if JMAT_MAX_POINT_LIGHTS > 0

        #if __VERSION__ >= 300
        
            // Offset directions for sampling point shadows
            const vec3 g_gridSamplingDisk[20] = vec3[]
            (
                vec3(1, 1,  1), vec3( 1, -1,  1), vec3(-1, -1,  1), vec3(-1, 1,  1),
                vec3(1, 1, -1), vec3( 1, -1, -1), vec3(-1, -1, -1), vec3(-1, 1, -1),
                vec3(1, 1,  0), vec3( 1, -1,  0), vec3(-1, -1,  0), vec3(-1, 1,  0),
                vec3(1, 0,  1), vec3(-1,  0,  1), vec3( 1,  0, -1), vec3(-1, 0, -1),
                vec3(0, 1,  1), vec3( 0, -1,  1), vec3( 0, -1, -1), vec3( 0, 1, -1)
            );

        #endif

        float jop_CalculatePointShadow(const in vec3 lightDir, const in float farPlane, const in samplerCube samp)
        {
            // Test for shadows with PCF
            #if __VERSION__ >= 300

                // Get current linear depth as the length between the fragment and light position
                float currentDepth = length(lightDir);

                float shadow = 0.0;
                const float bias = 0.15;
                const int samples = 20;

                float viewDistance = length(u_CameraPosition - vf_FragPosition);
                float diskRadius = (1.0 + (viewDistance / farPlane)) / 25.0;
                for (int i = 0; i < samples; ++i)
                {
                    vec3 sampCoords = lightDir + g_gridSamplingDisk[i] * diskRadius;
                    
                    float closestDepth = JOP_TEXTURE_CUBE(samp, sampCoords).r;

                    // Undo mapping [0,1]
                    closestDepth *= farPlane;

                    if (currentDepth - bias > closestDepth)
                        shadow += 1.0;
                }
                shadow /= float(samples);

                return shadow;

            #else

                float closestDepth = JOP_TEXTURE_CUBE(samp, lightDir).r;

                closestDepth *= farPlane;

                float currentDepth = length(lightDir);

                const float bias = 0.05;

                return currentDepth - bias > closestDepth ? 1.0 : 0.0;

            #endif

        }

    #endif

    #if JMAT_MAX_DIRECTIONAL_LIGHTS > 0 || JMAT_MAX_SPOT_LIGHTS > 0

        // Shadow calculation for directional and spot lights
        float jop_CalculateDirSpotShadow(const in vec3 projCoords, const in vec3 norm, const in vec3 lightDir, const in sampler2D samp)
        {
            // Get closest depth value from light's perspective (using [0,1] range fragPosLight as coords)
            float closestDepth = JOP_TEXTURE_2D(samp, projCoords.xy).r;

            // Get depth of current fragment from light's perspective
            float currentDepth = projCoords.z;

            // Check whether current frag pos is in shadow
            float bias = max(0.05 * (1.0 - dot(norm, lightDir)), 0.005);

            float shadow = 0.0;

            if (projCoords.z > 1.0)
                shadow = 0.0;

            // Do percentage-closer filtering
            else
            {
            #if __VERSION__ >= 300

                vec2 texelSize = vec2(1.0) / vec2(textureSize(samp, 0));
                for(int x = -1; x <= 1; ++x)
                {
                    for(int y = -1; y <= 1; ++y)
                    {
                        float pcfDepth = JOP_TEXTURE_2D(samp, projCoords.xy + vec2(x, y) * texelSize).r;
                        shadow += currentDepth - bias > pcfDepth ? 1.0 : 0.0;        
                    }    
                }
                shadow /= 9.0;

            #else

                shadow = float(currentDepth - bias > closestDepth);

            #endif
            }

            return shadow;
        }

    #endif

#pluginend